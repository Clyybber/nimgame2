<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="utf-8"/>

    <title>Nimgame 2</title>

    <link rel="stylesheet" type="text/css" href="style.css" media="screen"/>
</head>
<body>

<noscript>This site needs JavaScript to work properly.</noscript>

<header>
</header>

<main>

    <aside>
    </aside>

    <article>
        <h2>PLATFORMER GAME</h2>

<div class="section" id="introduction"><h3>Introduction</h3>

</div>


<div class="section" id="assets"><h3>Assets</h3>

</div>


<div class="section" id="assets"><h3>Starting to Code</h3>

    <p>
        First lines of code are actually pretty similar to the <a href="tut101_bounce.html"></a> so we won't dive too deep here this time.
    </p>

<h4>data.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    assets,
    audio,
    font,
    mosaic,
    scene,
    texturegraphic,
    truetypefont,
    types,
  ]


const
  GameWidth* = 640
  GameHeight* = 360
  GameTitle* = "Nimgame 2 Platformer"


var
  titleScene*, mainScene*: Scene
  defaultFont*, bigFont*: TrueTypeFont
  gfxData*: Assets[TextureGraphic]
  sfxData*: Assets[Sound]
  buttonMosaic*: Mosaic
  buttonSkin*: TextureGraphic
  score*: int


proc loadData*() =
  defaultFont = newTrueTypeFont()
  if not defaultFont.load("data/fnt/FSEX300.ttf", 16):
    echo "ERROR: Can't load font"
  bigFont = newTrueTypeFont()
  if not bigFont.load("data/fnt/FSEX300.ttf", 32):
    echo "ERROR: Can't load font"

  gfxData = newAssets[TextureGraphic](
    "data/gfx",
    proc(file: string): TextureGraphic = newTextureGraphic(file))

  sfxData = newAssets[Sound](
    "data/sfx",
    proc(file: string): Sound = newSound(file))

  buttonMosaic = newMosaic("data/gui/button.png", (8, 8))
  buttonSkin = newTextureGraphic()
  discard buttonSkin.assignTexture buttonMosaic.render(
    patternStretchBorder(8, 2))


proc freeData*() =
  defaultFont.free()
  bigFont.free()
  for graphic in gfxData.values:
    graphic.free()
  for sound in sfxData.values:
    sound.free()
  buttonSkin.free()
  buttonMosaic.free()

</pre></div>


<h4>title.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    assets,
    entity,
    font,
    gui/button,
    gui/widget,
    mosaic,
    nimgame,
    scene,
    settings,
    textgraphic,
    texturegraphic,
    types,
  ],
  data


type
  TitleScene = ref object of Scene


proc init*(scene: TitleScene) =
  init Scene(scene)

  # Title text
  let titleText = newTextGraphic bigFont
  titleText.setText GameTitle
  let title = newEntity()
  title.graphic = titleText
  title.centrify()
  title.pos = (GameWidth / 2, GameHeight / 3)
  scene.add title


proc free*(scene: TitleScene) =
  discard


proc newTitleScene*(): TitleScene =
  new result, free
  init result


method event*(scene: TitleScene, event: Event) =
  scene.eventScene event
  if event.kind == KeyDown:
    case event.key.keysym.sym:
    of K_Space, K_Return:
      game.scene = mainScene # quick start
    else:
      discard

</pre>

<h4>main.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    assets,
    entity,
    font,
    gui/button,
    gui/widget,
    nimgame,
    scene,
    textgraphic,
    types,
  ],
  data


type
  MainScene = ref object of Scene


proc init*(scene: MainScene) =
  init Scene scene


proc free*(scene: MainScene) =
  discard


proc newMainScene*(): MainScene =
  new result, free
  init result


method event*(scene: MainScene, event: Event) =
  scene.eventScene event
  if event.kind == KeyDown:
    case event.key.keysym.sym:
    of K_F10:
      colliderOutline = not colliderOutline
    of K_F11:
      showInfo = not showInfo
    else: discard

</pre>



<h4>platformer.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    nimgame,
    settings,
    types,
  ],
  data,
  title,
  main


game = newGame()
if game.init(GameWidth, GameHeight, title = GameTitle, integerScale = true):

  # Init
  game.setResizable(true) # Window could be resized
  game.minSize = (GameWidth, GameHeight) # Minimal window size
  game.windowSize = (GameWidth * 2, GameHeight * 2) # Doulbe scaling (1280x720)
  game.centrify() # Place window at the center of the screen

  loadData() # Call it before any scene initialization

  # Create scenes
  titleScene = newTitleScene()
  mainScene = newMainScene()

  # Run
  game.scene = titleScene # Initial scene
  run game # Let's go!
</pre>

</div>


<div class="section" id="menu"><h3>Menu</h3>

    <p>
        Let's add "Play" and "Exit" buttons to our title screen. This buttons will work through the action procedures feature of <b>GuiWidget</b> type.
    </p>

<h4>title.nim</h4>
<pre class="prettyprint">
...

type
  TitleScene = ref object of Scene


# Play action procedure
proc play(widget: GuiWidget) =
  game.scene = mainScene


# Exit action procedure
proc exit(widget: GuiWidget) =
  gameRunning = false

...
</pre>

    <p>
        Now, we just need to initialize both buttons in the <b>init</b> procedure, binding the actions.
    </p>

<h4>title.nim</h4>
<pre class="prettyprint">
...

proc init*(scene: TitleScene) =
  init Scene(scene)

  # Create menu buttons
  var
    btnPlay, btnExit: GuiButton
    btnPlayLabel, btnExitLabel: TextGraphic

  # Play button
  btnPlayLabel = newTextGraphic defaultFont
  btnPlayLabel.setText "PLAY"
  btnPlay = newGuiButton(buttonSkin, btnPlayLabel)
  btnPlay.centrify()
  btnPlay.pos = (GameWidth / 2, GameHeight / 2)
  btnPlay.action = play # assign the action procedure
  scene.add btnPlay

  # Exit button
  btnExitLabel = newTextGraphic defaultFont
  btnExitLabel.setText "EXIT"
  btnExit = newGuiButton(buttonSkin, btnExitLabel)
  btnExit.centrify()
  btnExit.pos = (GameWidth / 2, GameHeight / 2 + 64)
  btnExit.action = exit # assign the action procedure
  scene.add btnExit

  ...
</pre>

</div>


<div class="section" id="level"><h3>Level</h3>

    <p>
        Now we'll create a wireframe for our level module.
    </p>

<h4>level.nim</h4>
<pre class="prettyprint">
import
  parseutils,
  nimgame2 / [
    assets,
    entity,
    texturegraphic,
    tilemap,
    utils,
  ],
  data


const
  TileDim* = (32, 32)


type
  Level* = ref object of TileMap


proc init*(level: Level, tiles: TextureGraphic) =
  init Tilemap level
  level.tags.add("level")
  level.graphic = tiles
  level.initSprite(TileDim)


proc newLevel*(tiles: TextureGraphic): Level =
  new result
  result.init(tiles)


proc load*(level: Level, csv: string) =
  level.map = loadCSV[int](
    csv,
    proc(input: string): int = discard parseInt(input, result))

  level.hidden.add @[8, 9, 10, 11]  # tiles on a third row are invisible markers
  level.passable.add @[0, 2, 3, 4, 8, 9, 10, 11] # tiles without colliders
  level.initCollider()

</pre>

    <p>
        Now we could add the level into the main scene.
    </p>

<h4>main.nim</h4>
<pre class="prettyprint">
import
  ...
  data,
  level

...

type
  MainScene = ref object of Scene
    level: Level

proc init*(scene: MainScene) =
  ...

  # Level
  scene.level = newLevel gfxData["tiles"]
  scene.level.load "data/csv/map1.csv"
  scene.level.layer = LevelLayer
  scene.add scene.level

</pre>

    <p>
        You should see top-left corner of the map on the main screen at the moment.
    </p>

</div>


<div class="section" id="player"><h3>Player</h3>

    <p>
        The next step is to create a player entity.
    </p>

<h4>player.nim</h4>
<pre class="prettyprint">
import
  nimgame2 / [
    collider,
    entity,
    texturegraphic,
    tilemap,
    types,
  ],
  data,
  level


const
  Framerate = 1/12
  VisibilityDim: Dim = (w: 10, h: 6)
  Spawn = 8 # player spawn selector tile index
  PlayerRadius = 16
  PlayerSize = PlayerRadius * 2


type
  Player* = ref object of Entity
    level*: Level


proc updateVisibility*(player: Player) =
  # update the visible portion of the map
  let
    center = player.level.tileIndex(player.pos)
  player.level.show = (
    x: (center.x - VisibilityDim.w)..(center.x + VisibilityDim.w),
    y: (center.y - VisibilityDim.h)..(center.y + VisibilityDim.h))


proc resetPosition*(player: Player) =
  # reset player position to a given tile
  player.pos = player.level.tilePos player.level.firstTileIndex(Spawn)


proc init*(player: Player, graphic: TextureGraphic, level: Level) =
  player.initEntity()
  player.tags.add "player"
  player.level = level
  player.graphic = graphic
  player.initSprite((PlayerSize, PlayerSize))
  discard player.addAnimation("right", [0, 1, 2, 3], Framerate)
  discard player.addAnimation("left", [0, 1, 2, 3], Framerate, Flip.horizontal)
  discard player.addAnimation("death", [4, 5, 6, 7], Framerate)


proc newPlayer*(graphic: TextureGraphic, level: Level): Player =
  new result
  result.init(graphic, level)


method update*(player: Player, elapsed: float) =
  player.updateEntity elapsed
  player.updateVisibility()

</pre>

    <p>
        Adding it to the scene is trivial.
    </p>

<h4>main.nim</h4>
<pre class="prettyprint">
import
  ...
  level,
  player

const
  LevelLayer = 0
  PlayerLayer = 10


type
  MainScene = ref object of Scene
    level: Level
    player: Player


proc init*(scene: MainScene) =
  init Scene scene

  # Camera
  scene.camera = newEntity()
  scene.cameraBondOffset = game.size / 2  # set camera to the center

  ...

  # Player
  scene.player = newPlayer(gfxData["player"], scene.level)
  scene.player.collisionEnvironment = @[Entity(scene.level)]
  scene.player.layer = PlayerLayer
  scene.player.resetPosition()
  scene.add scene.player

  scene.cameraBond = scene.player # bind camera to the player entity
  scene.player.updateVisibility()

...
</pre>

</div>


<div class="section" id="player_movement"><h3>Player Movement</h3>

    <p>
        To make the player entity move, we will use the platformerPhysics procedure from the engine's entity module.
    </p>

<h4>player.nim</h4>
<pre class="prettyprint">
const
  ...
  ColliderRadius = PlayerRadius - 1
  GravAcc = 1000
  Drag = 800
  JumpVel = 450
  WalkVel = 10

...

proc init*(player: Player, graphic: TextureGraphic, level: Level) =
  ...


  # collider
  let c = newGroupCollider(player)
  player.collider = c
  # 1st collider
  c.list.add newCircleCollider(
    player,
    (PlayerRadius, PlayerRadius),
    ColliderRadius)
  # 2nd collider
  c.list.add newBoxCollider(
    player,
    (PlayerRadius, PlayerRadius + PlayerRadius div 2),
    (PlayerSize - 2, ColliderRadius))

  # physics
  player.acc.y = GravAcc
  player.drg.x = Drag
  player.physics = platformerPhysics

...

proc jump*(player: Player) =
  if player.vel.y == 0.0:
    player.vel.y -= JumpVel


proc right*(player: Player) =
  player.vel.x += WalkVel
  if not player.sprite.playing and player.vel.y == 0.0:
    player.play("right", 1)


proc left*(player: Player) =
  player.vel.x -= WalkVel
  if not player.sprite.playing and player.vel.y == 0.0:
    player.play("left", 1)

...
</pre>

    <p>
        Next, we bind movement action to keyboard controls.
    </p>

<h4>main.nim</h4>
<pre class="prettyprint">
...

method update*(scene: MainScene, elapsed: float) =
  scene.updateScene elapsed
  if ScancodeSpace.pressed:
    scene.player.jump()
  if ScancodeRight.down:
    scene.player.right()
  if ScancodeLeft.down:
    scene.player.left()

</pre>

</div>


<div class="section" id="dying_and_spikes"><h3>Dying and Spikes</h3>

<h4>main.nim</h4>
<pre class="prettyprint">
...

const
  ...
  Spikes = 4 # Spikes tile index

...

proc init*(scene: MainScene) =
  ...

  # Spikes
  const
    SpikesOrigin = TileDim / 2 + (0, TileDim[1] div 4)
    SpikesDim = TileDim / (1, 2)
  for tileCoord in scene.level.tileIndex(Spikes):
    let e = newEntity()
    e.tags.add "spikes"
    e.pos = scene.level.tilePos(tileCoord)
    e.collider = newBoxCollider(e, SpikesOrigin, SpikesDim)
    e.collider.tags.add "player" # collide only with player entity
    scene.add e

  ...
</pre>


<h4>player.nim</h4>
<pre class="prettyprint">
...

type
  Player* = ref object of Entity
    ...
    dying: bool

...

proc die*(player: Player) =
  if not player.dying:
    player.dying = true
    player.play("death", 3)
    player.vel.y = -JumpVel


method update*(player: Player, elapsed: float) =
  ...

  if player.dying:
    if not player.sprite.playing:
      # reset
      player.play("right", 0)
      player.resetPosition()
      player.updateVisibility()
      player.dying = false
    else:
      return


method onCollide*(player: Player, target: Entity) =
  if "spikes" in target.tags:
    player.die()

</pre>

</div>


<div class="section" id="boxes_and_coins"><h3>Boxes and Coins</h3>

<h4>player.nim</h4>
<pre class="prettyprint">
...

type
  Player* = ref object of Entity
    ...
    requestCoins*: seq[CoordInt]

...

proc init*(player: Player, graphic, TextureGraphic, level: Level) =
  ...

  player.requestCoins = @[]


...

method onCollide*(player: Player, target: Entity) =
  ...

  if "box" in target.tags:
    let index = player.level.tileIndex(target.pos)
    player.level.tile(index) += 1 # red box -&gt; grey box
    player.requestCoins.add index + (0, -1) # request coin spawn on tile higher
    target.dead = true

  if "coin" in target.tags:
    inc score
    target.dead = true

</pre>

<h4>main.nim</h4>
<pre class="prettyprint">
...

proc init*(scene: MainScene) =
  ...

  # Boxes
  const
    BoxPos1 = (2, TileDim[1] + 2)
    BoxPos2 = (TileDim[0] - 2, TileDim[1] + 2)
  for tileCoord in scene.level.tileIndex(Box):
    let e = newEntity()
    e.tags.add "box"
    e.pos = scene.level.tilePos(tileCoord)
    e.collider = newLineCollider(e, BoxPos1, BoxPos2)
    e.collider.tags.add "player" # collide only with player entity
    scene.add e

  ...

...

method update*(scene: MainScene, elapsed: float) =
  ...

  # Spawn coins
  while scene.player.requestCoins.len &gt; 0:
    let e = newEntity()
    e.tags.add "coin"
    e.graphic = gfxData["tiles"]
    e.initSprite(TileDim)
    discard e.addAnimation("rotate", [2, 3], 1/8)
    e.play("rotate", -1)
    e.pos = scene.level.tilePos scene.player.requestCoins.pop()
    e.collider = newCircleCollider(e, TileDim / 2 - 1, TileDim[0] / 2 - 1)
    e.collider.tags.add "player"
    scene.add e

</pre>


</div>

<div class="section" id="links"><h3>Links</h3>
    <ul>
        <li>
            <a href="https://github.com/Vladar4/ng2platformer">Source on GitHub</a>
        </li>
    </ul>
</div>

<!--

<div class="section" id=""><h3></h3>

<h4></h4>
<pre class="prettyprint">

</pre>

</div>

-->

    </article>

    <aside style="display: flex; border: 0;"></aside>
</main>

<footer>
</footer>

<script src="structure.js"></script>

<script>
    createSectionsList();
</script>

<script src="https://cdn.rawgit.com/google/code-prettify/master/loader/run_prettify.js"></script>

</body>
</html>

